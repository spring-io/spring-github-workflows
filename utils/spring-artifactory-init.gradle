initscript {
	dependencies {
		classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:5.+'
	}
}

allprojects {
	tasks.register('artifactoryPublish', org.jfrog.gradle.plugin.artifactory.task.ArtifactoryTask) {
		publications('mavenJava')
	}
}

gradle.projectsLoaded {
	rootProject { root ->
		allprojects {
			try {
				artifactory {
					publish {
						defaults {
							def zipArtifactProps =
									['zip.name'       : project.name,
									 'zip.displayname': project.description,
									 'zip.deployed'   : 'false']
							properties {
								mavenJava zipArtifactProps, '*:*:*:*@zip'
								mavenJava 'zip.type': 'docs', '*:*:*:docs@zip'
								mavenJava 'zip.type': 'dist', '*:*:*:dist@zip'
							}
						}
						forkCount = 10
					}
					clientConfig.setConnectionRetries(4)
				}
			}
			catch (exception) {
				root.logger.debug('Ignore `artifactory` configuration. Probably no `ArtifactoryPlugin` provided', exception);
			}
		}
	}
}
